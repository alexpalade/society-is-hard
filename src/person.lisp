(cl:in-package :sih)

(defclass person ()
  ((destination :initform nil :accessor destination)
   (path :initform nil :accessor path)
   (target :initform nil :accessor target)
   (state :initform 'wander :accessor state)
   (rest-time :initform (+ 0.5 (/ 1 (+ 0.1 (random 10)))) :accessor rest-time)
   (row :initform nil :accessor row)
   (col :initform nil :accessor col)
   (health :initform 100 :accessor health)
   (sick :initform nil :accessor sick)
   (medicine :initform 0 :accessor medicine)
   (last-cough-time :initform nil :accessor last-cough-time)
   (last-move-time :initform (/ 1 (1+ (random 10))) :accessor last-move-time)))

(defmethod state-p ((this person) state)
  (equal (state this) state))

(defmethod regular-person-p ((person person))
  (not (or (typep person 'police)
           (typep person 'medic))))

(defmethod become-sick ((this person))
  (when (not (sick this))
    (setf (last-cough-time this) (real-time-seconds))
    (setf (medicine this) 0)
    (setf (sick this) T)))

(defmethod become-healthy ((this person))
  (setf (health this) 100)
  (setf (sick this) nil))

(defmethod cough ((this person))
  (decf (health this) 0.5)
  (setf (last-cough-time this) (real-time-seconds)))

(defmethod move-person ((game sih) person to-row to-col)
  (let ((from-row (row person))
        (from-col (col person))
        (cells (cells game)))
    (setf (aref cells from-row from-col) nil)
    (setf (aref cells to-row to-col) person)
    (setf (row person) to-row)
    (setf (col person) to-col)
    (setf (last-move-time person) (real-time-seconds))))

(defmethod tick ((game sih) (person person))
  (when (sick person)
    (when (> (- (real-time-seconds) (last-cough-time person)) 0.2)
      (cough person)
      (when (<= (health person) 0)
        (remove-person game person)
        (play :death)))
    (dolist (near-person (get-near-persons game person))
      (when (= 0 (random 500))
        (become-sick near-person))))
  (with-slots (rest-time last-move-time) person
    (when (and
           (state-p person 'wander)
           (> (- (real-time-seconds) last-move-time) rest-time))
        (let* ((to (get-random-move-cell game person))
               (to-row (first to))
               (to-col (second to)))
          (when to
            (setf (rest-time person) (/ (+ 1000 (random 2000)) 1000))
            (move-person game person to-row to-col))))))


(defmethod render-avatar ((this person) asset)
  (with-pushed-canvas ()
    (let* ((width (image-width asset))
           (height (image-height asset))
           (scale-for (max width height))
           (scale (/ (- *cell-size* *cell-padding*) scale-for))
           (scaled-cell-size (/ *cell-size* scale))
           (scale-no-padding (/ *cell-size* scale-for))
           (scaled-cell-size-no-padding (/ *cell-size* scale-no-padding)))
      (when (sick this)
        (let ((alpha (+ 0.5 (- 0.5 (/ (health this) 100)))))
          (draw-rect (vec2 0 0) *cell-size* *cell-size*
                     :fill-paint (vec4 0.9 0.1 0.1 alpha))))

      (scale-canvas scale scale)
      (draw-image
       (vec2 (- (/ scaled-cell-size 2) (/ width 2))
             (- (/ scaled-cell-size 2) (/ height 2)))
       asset))))

(defmethod render ((this person))
  (render-avatar this :person))
